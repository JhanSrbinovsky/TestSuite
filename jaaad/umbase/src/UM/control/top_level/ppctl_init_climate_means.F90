#if defined(CONTROL)
! *****************************COPYRIGHT*******************************
! (C) Crown copyright Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
! *****************************COPYRIGHT*******************************
!
!+ Initialises a climate mean PP file.

Subroutine PPCTL_INIT_CLIMATE_MEANS(     &
#include "argduma.h"
   I_AO,MEANLEV,PPNAME,ICODE,CMESSAGE )

  Implicit None
  !
  ! Description:
  !   Initialises a climate mean PP file.
  !
  ! Method:
  !   One file is initialised per call, based on the mean level and
  !   the submodel.
  !
  !  External documentation: UM documentation papers:
  !      C0 - The top-level control system; 
  !      C4 - Storage handling and diagnostic system
  !      C5 - Control of means calculations
  !
  ! Current Code Owner:
  !
  ! Code Description:
  !   Language: FORTRAN 90 + common extensions.
  !   This code is written to UMDP3 v6 programming standards.
  !
  !

#include "cmaxsize.h"
#include "cintfa.h"
#include "cmaxsizo.h"
#include "parvars.h"
#include "typsize.h"
#include "typduma.h"

#include "csubmodl.h"

  Integer        ,Intent(in   ) ::I_AO      ! Atmosphere/Ocean indicator
  Integer        ,Intent(in   ) ::MEANLEV   ! Mean level indicator
  Character*14   ,Intent(  out) ::PPNAME    ! PP filename generated by GET_NAME
  Integer        ,Intent(  out) ::ICODE     ! Return code from routine
  Character*(80) ,Intent(  out) ::CMESSAGE  ! Return message if failure occurred
  !
  !*----------------------------------------------------------------------
  !  Common blocks
  !
#include "chsunits.h"
#include "chistory.h"
#include "ccontrol.h"
#include "ctime.h"
#include "cenvir.h"

  !
  !  Local variables
  !
  Integer       ::NFTUNIT          ! FORTRAN unit
  Integer       ::SMID             ! Submodel id for filenaming
  Integer       ::LEN_PPNAME       ! Length of filename
  Character*80  ::STRING           ! Work array
  ! ----------------------------------------------------------------------

  ! ---Arguments to IO Routines ---
  ! Fourth argument to File_Open defines whether file is to be opened 
  ! read-write or read-only - all files opened read-write in this subroutine
  Integer,Parameter :: OpenReadWrite              = 1
  ! Fifth arguments to File_Open and fourth to File_Close defines whether 
  ! file name is provided  explicitly or whether it is within environment 
  ! variable in the second argument. All names provided explicitly here 
  Integer,Parameter :: FileNameProvidedExplicitly = 1
  ! Fifth argument to file_close indicates whether file should be deleted
  ! after closing - no files are deleted from this subroutine
  Integer, Parameter :: DoNotDeleteFile = 0
  ! Some arguments to get_name subroutine are always the same from here
  ! and so are set by parameters
  Integer,Parameter ::AnalysisHours = 0 
  Integer,Parameter ::Toggle        = 1

  SMID=I_AO
  If (SMID == a_im) Then
    NFTUNIT = FT_MEANim(A_IM)
  Else
    write(6,*)'PPCTL_INTI_CLIMATE_MEANS: UM only supports Atmos model'
    ICODE = 1
    Go To 9999
  End If

  ! 
  !  1.1   Construct PPfile name from model information using defined
  !        naming convention (OUTPUT files)
  ! 
  ! DEPENDS ON: get_name
  Call GET_NAME(EXPT_ID,JOB_ID,SMID,MEANLEV,TOGGLE,       &
     FT_STEPS(NFTUNIT),TYPE_LETTER_1(NFTUNIT),            &
     TYPE_LETTER_3(NFTUNIT),MODEL_STATUS,TIME_CONVENTION, &
     AnalysisHours,PPNAME,ICODE,CMESSAGE,                 &
     LCAL360)
  If (ICODE >  0) Goto 9999

  ! 
  !  1.2 Open named file on unit NFTUNIT
  ! 
  LEN_PPNAME=Len(PPNAME)
  ! DEPENDS ON: file_close
  Call FILE_CLOSE(NFTUNIT,PPNAME,LEN_PPNAME,FileNameProvidedExplicitly, &
     DoNotDeleteFile,ICODE)
  Write(6,*)'PPCTL:1 Opening new file ',PPNAME,' on unit ',NFTUNIT
  LEN_PPNAME=Len(PPNAME)
  ! DEPENDS ON: file_open
  Call FILE_OPEN(NFTUNIT,PPNAME,LEN_PPNAME,FileNameProvidedExplicitly,  &
     OpenReadWrite,ICODE)
  If (ICODE /= 0) Then
    CMESSAGE='PPCTL   : Error opening new PPfile'
    Goto 9999   !  Return
  End If

  ! 
  !  1.2.1 Update history file record
  ! 
  STRING=MODEL_FT_UNIT(NFTUNIT)
  ! Check that a name exists in STRING(1:8), if not set from CENVIRDT
  If (STRING(1:8)  ==  '        ') Then
    STRING(1:8) = FT_ENVIRON(NFTUNIT)
    STRING(9:9) = ':'
  End If
  STRING(11:17)='$DATAM/'
  STRING(18:31)=PPNAME
  STRING(32:80)='                                          '
  MODEL_FT_UNIT(NFTUNIT)=STRING

  ! 
  !  1.3 Initialise the direct access LOOKUP headers (OUTPUT file)
  ! 

#if defined(ATMOS)
  If (SMID == a_im) Then
    Write(6,*)'PPCTL:1 Initialising new file on unit ',NFTUNIT
    ! DEPENDS ON: init_pp
    Call INIT_PP(NFTUNIT,TYPE_LETTER_1(NFTUNIT),         &
       LEN1_LOOKUP,PP_LEN2_MEANim(MEANLEV,A_IM),         &
       A_FIXHD,A_INTHD,A_REALHD,A_LEVDEPC,               &
       A_ROWDEPC,A_COLDEPC,                              &
       LEN_FIXHD,A_LEN_INTHD,A_LEN_REALHD,A_LEN1_LEVDEPC,&
       A_LEN2_LEVDEPC,A_LEN1_ROWDEPC,A_LEN2_ROWDEPC,     &
       A_LEN1_COLDEPC,A_LEN2_COLDEPC,                    &
       PP_LEN_INTHD, PP_LEN_REALHD,                      &
       ICODE,CMESSAGE)       
    If (ICODE >  0) Goto 9999
    FT_LASTFIELD(NFTUNIT)=0
  End If
#endif

  !
  ! 1.3.1 Close new file and exit
  !

  LEN_PPNAME=Len(PPNAME)
  ! DEPENDS ON: file_close
  Call FILE_CLOSE(NFTUNIT,PPNAME,LEN_PPNAME,FileNameProvidedExplicitly, &
     DoNotDeleteFile,ICODE)

  !-----------------------------------------------------------------------
9999 Return
End Subroutine PPCTL_INIT_CLIMATE_MEANS
#endif
