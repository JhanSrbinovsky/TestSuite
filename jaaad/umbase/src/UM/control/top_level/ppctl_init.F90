#if defined(CONTROL)
! *****************************COPYRIGHT*******************************
! (C) Crown copyright Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
! *****************************COPYRIGHT*******************************
!
!+ Initialises the setup of fieldsfiles at the beginning of an NRUN or CRUN

Subroutine PPCTL_INIT(            &
#include "argduma.h"
#include "arginfa.h"
   I_AO,PPNAME,ICODE,CMESSAGE )
  !
  Implicit None
  !
  ! Description:
  !   Initialises the setup of fieldsfiles at the beginning of an NRUN or CRUN
  !
  ! Method:
  !           1. Opens all active input and output files on initial
  !              call to routine.
  !           2. Files to be processed on each call are controlled by
  !              the switch LPP_SELECT set on step 0 and at regular
  !              intervals thereafter.
  !           3. All active PP or boundary output files are
  !              intialised with user-specified number of LOOKUP headers
  !              (fields).
  !           4. All boundary output files are accessed.
  !
  ! Current Code Owner:
  !
  ! Code Description:
  !   Language: FORTRAN 90 + common extensions.
  !   This code is written to UMDP3 v6 programming standards.
  !

#include "cmaxsize.h"
#include "cintfa.h"
#include "cmaxsizo.h"
#include "parvars.h"
#include "typsize.h"
#include "typduma.h"
#include "typinfa.h"

#include "csubmodl.h"

  Integer        ,Intent(in   ) ::I_AO     ! Atmosphere/Ocean indicator
  Character*14   ,Intent(  out) ::PPNAME   ! PP filename generated by GET_NAME
  Integer        ,Intent(  out) ::ICODE    ! Return code from routine
  Character*(80) ,Intent(  out) ::CMESSAGE ! Return message if failure occurred
  !
  !*--------------------------------------------------------------
  !  Common blocks
  !
#include "chsunits.h"
#include "chistory.h"
#include "ccontrol.h"
#include "ctime.h"
#include "cenvir.h"

  !
  !  Local variables
  !
  Integer       ::NFTUNIT          ! FORTRAN unit
  Integer       ::SMID             ! Submodel id for filenaming
  Integer       ::SECS_PER_STEP    ! Seconds per model timestep
  Integer       ::STEP             ! Timestep of model
  Integer       ::LEN_PPNAME       ! Length of file name
  Integer       ::JINTF            ! Interface area index
  Character*80  ::STRING           ! Work array
  Character*17  ::LOCAL_TC         ! Local version of TIME_CONVENTION

  ! ---Arguments to IO Routines ---
  ! Fourth argument to File_Open defines whether file is to be opened 
  ! read-write or read-only - all files opened read-write in this subroutine
  Integer,Parameter :: OpenReadWrite              = 1
  ! Fifth arguments to File_Open and fourth to File_Close defines whether 
  ! file name is provided  explicitly or whether it is within environment 
  ! variable in the second argument 
  Integer,Parameter :: FileNameInEnvVar           = 0
  Integer,Parameter :: FileNameProvidedExplicitly = 1
  ! Fifth argument to file_close indicates whether file should be deleted
  ! after closing - no files are deleted from this subroutine
  Integer, Parameter :: DoNotDeleteFile = 0
  ! Some arguments to get_name subroutine are always the same from here
  ! and so are set by parameters
  Integer,Parameter ::MeanLevel     = 0 
  Integer,Parameter ::AnalysisHours = 0 
  Integer,Parameter ::Toggle        = 1


  ! Get seconds per timestep and current timestep number.
  If (I_AO == a_im) Then
    SECS_PER_STEP = SECS_PER_STEPim(a_im)
    STEP=STEPim(a_im)
  Else
    write(6,*)'PPCTL_INIT: UM only supports Atmos model'
  End If

  Do NFTUNIT=20,NUNITS
    ! 
    !  1.1 OPEN all active units using most recent filenames (as
    !      assigned by script).
    !      Close unit again if reinitialisation is indicated to
    !      prevent heap fragmentation later.
    ! 
    If (FT_ACTIVE(NFTUNIT) == 'Y') Then

      ! A CRUN will start with active files that exist on disk. This
      ! call reads the file's lookup and header to initialises the lookup 
      ! and header arrays.
      ! DEPENDS ON: init_pp_crun
      Call init_pp_crun(nftunit,ft_environ(nftunit), &
         len_ft_envir(nftunit),len1_lookup,          &
         pp_len2_look(nftunit),len_fixhd,            &
         type_letter_1(nftunit))

      
      If(FT_STEPS(NFTUNIT) >  0) Then
        Write(6,*)'PPCTL:0 Closin file ',FT_ENVIRON(NFTUNIT),' on unit ', NFTUNIT
        ! DEPENDS ON: file_close
        Call FILE_CLOSE(NFTUNIT,FT_ENVIRON(NFTUNIT),                    &
           LEN_FT_ENVIR(NFTUNIT),FileNameInEnvVar,DoNotDeleteFile,ICODE)
      End If
    End If
    ! 
    !  1.2 Take action on all selected units
    ! 
    If (LPP_SELECT(NFTUNIT)) Then
      Write(6,*)'PPCTL:1 Closing file ',FT_ENVIRON(NFTUNIT),' on unit ', NFTUNIT
      ! DEPENDS ON: file_close
      Call FILE_CLOSE(NFTUNIT,FT_ENVIRON(NFTUNIT),                    &
         LEN_FT_ENVIR(NFTUNIT),FileNameInEnvVar,DoNotDeleteFile,ICODE)

      ! Set SMID from TYPE_LETTER_2

      If (TYPE_LETTER_2(NFTUNIT) == 'a') Then
        SMID = a_im
      Else
        SMID=I_AO
      End If

      ! 
      !  1.3 Open reinitialised files provided not already active.
      !      Filename required.
      ! 
      If (FT_STEPS(NFTUNIT) /= 0 .And.         &
         .Not. FT_ACTIVE(NFTUNIT) == 'Y') Then

        ! 
        !  1.4 Construct filename.
        ! 
        !  For reinitialised files whose reinitialisation period 
        !  is not a multiple of one hour, use 'Timestep' or 
        !  'Sub-hourly' filenaming convention rather than the 
        !  globally defined one.
        ! 

        If (FT_STEPS(NFTUNIT) >  0 .And.                         &
           Mod(FT_STEPS(NFTUNIT)*SECS_PER_STEP,3600) /= 0) Then
          If (FT_STEPS(NFTUNIT)*SECS_PER_STEP >  3600) Then
            LOCAL_TC = 'Timestep         '
          Else
            ! Use "Sub-hourly" filenaming time convention
            LOCAL_TC = 'Sub-hourly       '
          End If
        Else
          LOCAL_TC = TIME_CONVENTION
        End If
        ! 
        !  1.4.1 Input files.
        ! 
        If (FT_INPUT(NFTUNIT) == 'Y') Then
          ! DEPENDS ON: get_name
          Call GET_NAME(EXPT_ID_IN,JOB_ID_IN,SMID,MeanLevel,   &
             TOGGLE,FT_STEPS(NFTUNIT),TYPE_LETTER_1(NFTUNIT),  &
             TYPE_LETTER_3(NFTUNIT), MODEL_STATUS,LOCAL_TC,    &
             AnalysisHours,PPNAME,ICODE,CMESSAGE,              &
             LCAL360)
          If (ICODE >  0) Goto 9999
        Else
          ! 
          !  1.4.2 Output files.
          ! 
          ! DEPENDS ON: get_name
          Call GET_NAME(EXPT_ID,JOB_ID,SMID,MeanLevel,TOGGLE,  &
             FT_STEPS(NFTUNIT),TYPE_LETTER_1(NFTUNIT),         &
             TYPE_LETTER_3(NFTUNIT),MODEL_STATUS,LOCAL_TC,     &
             AnalysisHours,PPNAME,ICODE,CMESSAGE,              &
             LCAL360)
          If (ICODE >  0) Goto 9999
        End If

        Write(6,*)'PPCTL:2 Opening new file ',PPNAME,' on unit ', NFTUNIT

        LEN_PPNAME=Len(PPNAME)
        ! DEPENDS ON: file_open
        Call FILE_OPEN(NFTUNIT,PPNAME,LEN_PPNAME,OpenReadWrite, &
           FileNameProvidedExplicitly,ICODE)
        If (ICODE /= 0) Then
          CMESSAGE='PPCTL   : Error opening new PPfile'
          GO TO 9999   ! Return
        End If

        ! 
        !  1.6 Update history file record for appropriate unit 
        !      with new filename
        ! 
        STRING=MODEL_FT_UNIT(NFTUNIT)
        STRING(11:17)='$DATAM/'
        STRING(18:31)=PPNAME
        STRING(32:80)= '                                          '
        MODEL_FT_UNIT(NFTUNIT)=STRING
      End If

      ! 
      !  1.7 Initialise or read in the direct access lookup 
      !      headers of the input/output file.
      ! 
      !  (a)  PP files
      If (TYPE_LETTER_1(NFTUNIT) == 'p' .Or. &
         TYPE_LETTER_1(NFTUNIT) == 'c') Then

        If (FT_OUTPUT(NFTUNIT) == 'Y'               &
           .And..Not. FT_ACTIVE(NFTUNIT) == 'Y') Then
#if defined(ATMOS)
          If (SMID == a_im) Then
            ! 
            !  Open file if not to be re-initialised, 
            !  i.e. file name is in environment variable.
            If(FT_STEPS(NFTUNIT) == 0) Then
              ! DEPENDS ON: file_open
              Call FILE_OPEN(NFTUNIT,FT_ENVIRON(NFTUNIT),                   &
                 LEN_FT_ENVIR(NFTUNIT),OpenReadWrite,FileNameInEnvVar,ICODE)
            End If
            Write(6,*)'PPCTL:2 Initialising new file on unit ',NFTUNIT
            ! DEPENDS ON: init_pp
            Call INIT_PP(NFTUNIT,TYPE_LETTER_1(NFTUNIT),          &
               LEN1_LOOKUP,PP_LEN2_LOOK(NFTUNIT),                 &
               A_FIXHD,A_INTHD,A_REALHD,A_LEVDEPC,                &
               A_ROWDEPC,A_COLDEPC,                               &
               LEN_FIXHD,A_LEN_INTHD,A_LEN_REALHD,A_LEN1_LEVDEPC, &
               A_LEN2_LEVDEPC,A_LEN1_ROWDEPC,A_LEN2_ROWDEPC,      &
               A_LEN1_COLDEPC,A_LEN2_COLDEPC,PP_LEN_INTHD,        &
               PP_LEN_REALHD,ICODE,CMESSAGE)               
            If (ICODE >  0) Goto 9999
            FT_LASTFIELD(NFTUNIT)=0
          End If
#endif
        End If
        ! 
        !  (b) Boundary files input and output
        ! 
      Else If (TYPE_LETTER_1(NFTUNIT) == 'b') Then
        ! 
        !      Call boundary file initialisation routine even if a 
        !  continuation run and file already exists.
        ! 
        If (FT_OUTPUT(NFTUNIT) == 'Y') Then
          Write(6,*)'PPCTL: Opening boundary output file on unit ', NFTUNIT
          ! 
          !  Open file if not to be re-initialised, 
          !  i.e. file name is in environment variable.
          If(FT_STEPS(NFTUNIT) == 0) Then
            ! DEPENDS ON: file_open
            Call FILE_OPEN(NFTUNIT,FT_ENVIRON(NFTUNIT),                   &
               LEN_FT_ENVIR(NFTUNIT),OpenReadWrite,FileNameInEnvVar,ICODE)
          End If

          ! Get interface area number
          ! DEPENDS ON: intf_area
          Call intf_area( SMID, NFTUNIT, JINTF)

#if defined(ATMOS)
          If ( SMID  ==  a_im ) Then

            ! DEPENDS ON: in_intf
            Call IN_INTF (               &
#include "argduma.h"
#include "arginfa.h"
               NFTUNIT,ICODE,CMESSAGE)

            If (ICODE >  0) Then
              Write (6,*) ' PPCTL : Error in IN_INTF - Atmos.'
              Goto 9999  !  Return
            End If

          End If  !  if SMID
#endif
          If (FT_STEPS(NFTUNIT) >  0) Then
            If (STEP == 0 .Or.                                          &
                (                                                       &
                 STEP-FT_FIRSTSTEP(NFTUNIT) /= 0 .And.                  &
                 Mod(STEP-FT_FIRSTSTEP(NFTUNIT),FT_STEPS(NFTUNIT)) == 0 &
                )                                                       &
               ) Then
              FT_LASTFIELD(NFTUNIT)=0
            End If
          End If
          !      Call routine to open and read boundary input file.
          !             ELSE IF (FT_INPUT(NFTUNIT) == 'Y')then
          !      This is where IN_BOUND should be called
        End If

      End If

      ! Close unit to release IO buffer if later reinitialisation indicated
      If(FT_STEPS(NFTUNIT) /= 0)Then
        LEN_PPNAME=Len(PPNAME)
        Write(6,*)'PPCTL:2 Closing file ',PPNAME,' on unit ', NFTUNIT
        ! DEPENDS ON: file_close
        Call FILE_CLOSE(NFTUNIT,PPNAME,LEN_PPNAME,FileNameProvidedExplicitly, &
           DoNotDeleteFile,ICODE)
      End If
      !      Reset unit as active
      FT_ACTIVE(NFTUNIT) = 'Y'
    End If

  End Do

9999 Return
End Subroutine PPCTL_INIT
#endif
