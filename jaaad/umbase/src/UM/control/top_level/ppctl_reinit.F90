#if defined(CONTROL)
! *****************************COPYRIGHT*******************************
! (C) Crown copyright Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
! *****************************COPYRIGHT*******************************
!
!+ Reinitialises streams and sends old file for post-processing

Subroutine PPCTL_REINIT (        &
#include "argduma.h"
#include "arginfa.h"
     I_AO,PPNAME,ICODE,CMESSAGE )
  !
  Implicit None

  ! Description
  ! When a stream is due to be reinitialised, the old file is closed,
  ! the post-processing system is informed if requested, and a new file
  ! is initialised.
  !
  !  Method : 1. Files to be processed on each call are controlled by
  !              the switch LPP_SELECT set by ppctl_init and at regular
  !              intervals thereafter.
  !           2. Completed files are closed, and if post-processing is
  !              selected, a message is written via a unix pipe to the 
  !              post-processing server, to archive, delete etc. the file
  !           3. Where streams are to be reinitialised, a new file is 
  !              created with an appropriate name
  !
  ! Current Code Owner:
  !
  ! Code Description:
  !   Language: FORTRAN 90 + common extensions.
  !   This code is written to UMDP3 v6 programming standards.
  !

#include "cmaxsize.h"
#include "cintfa.h"
#include "cmaxsizo.h"
#include "parvars.h"
#include "typsize.h"
#include "typduma.h"
#include "typinfa.h"

#include "csubmodl.h"

  Integer        ,Intent(in   ) ::I_AO     ! Atmosphere/Ocean indicator
  Character*14   ,Intent(  out) ::PPNAME   ! PP filename generated by GET_NAME
  Integer        ,Intent(  out) ::ICODE    ! Return code from routine
  Character*(80) ,Intent(  out) ::CMESSAGE ! Return message if failure occurred
  !
  !----------------------------------------------------------------------
  !  Common blocks
  !
#include "chsunits.h"
#include "chistory.h"
#include "ccontrol.h"
#include "ctime.h"
#include "cenvir.h"

  !
  !  Local variables
  !
  Integer      :: NFTUNIT          ! FORTRAN unit
  Integer       ::SMID             ! Submodel id for filenaming
  Integer       ::I,IPOS           ! Indices within filename string
  Integer       ::SECS_PER_STEP    ! Seconds per model timestep
  Integer       ::STEP             ! Timestep of model
  Integer       ::LEN_PPNAME       ! Length of filename
  Integer       ::JINTF            ! Interface area index
  Character*80  ::STRING           ! Work array
  Character*14  ::OLDPPFILE        ! Previous PPfile on given unit
  Character*17  ::LOCAL_TC         ! Local version of TIME_CONVENTION
  !
  Character*80  ::filename         ! Used to hold the filename of the
  !                                  pipe file, so that the unit can be
  !                                  closed if necessary to flush it.

  ! ---Arguments to IO Routines ---
  ! Fourth argument to File_Open defines whether file is to be opened 
  ! read-write or read-only - all files opened read-write in this subroutine
  Integer,Parameter :: OpenReadWrite              = 1
  ! Fifth arguments to File_Open and fourth to File_Close defines whether 
  ! file name is provided  explicitly or whether it is within environment 
  ! variable in the second argument 
  Integer,Parameter :: FileNameInEnvVar           = 0
  Integer,Parameter :: FileNameProvidedExplicitly = 1
  ! Fifth argument to file_close indicates whether file should be deleted
  ! after closing - no files are deleted from this subroutine
  Integer, Parameter :: DoNotDeleteFile = 0
  ! Some arguments to get_name subroutine are always the same from here
  ! and so are set by parameters
  Integer,Parameter ::MeanLevel     = 0 
  Integer,Parameter ::AnalysisHours = 0 
  Integer,Parameter ::Toggle        = 1

  ! Get seconds per timestep and current timestep number.
  If (I_AO == a_im) Then
    SECS_PER_STEP = SECS_PER_STEPim(a_im)
    STEP=STEPim(a_im)
  Else
    write(6,*)'PPCTL_REINIT: UM only supports Atmos model'
  End If

  ! Get name of pipe
  Call get_file(8, filename, 80, icode)

  ! ----------------------------------------------------------------------
  !  1.1 Loop over all valid FORTRAN units and select those to be
  !      initialised this timestep as set by SETTSCTL
  ! 
  SMID=I_AO

  Do NFTUNIT=20,NUNITS

    If (LPP_SELECT(NFTUNIT)) Then
      ! 
      !  1.2   Generate output processing requests for previous file on
      !        this unit, which is now complete
      ! 
      STRING=MODEL_FT_UNIT(NFTUNIT)
      IPOS = 0
      Do I=1,80
        If (STRING(I:I) == "/") IPOS=I
      End Do
      OLDPPFILE=STRING(IPOS+1:IPOS+14)

      If (mype == 0) Then
        If (FT_SELECT(NFTUNIT) == 'Y' .or. FT_ARCHSEL(NFTUNIT) == 'Y') Then
          ! File is selected for deletion and/or archiving.
          !
          ! In case the file is open, we must make sure that the Lookup
          ! Table has been written, and the file closed

          ! DEPENDS ON: file_close
          Call FILE_CLOSE(NFTUNIT, MODEL_FT_UNIT(NFTUNIT),       &
             Len_trim(MODEL_FT_UNIT(NFTUNIT)), FileNameInEnvVar, &
             DoNotDeleteFile, ICODE)

          If (NFTUNIT >= 60  .And. NFTUNIT <= 69 .Or. NFTUNIT == 151) Then
            If (FT_ARCHSEL(NFTUNIT) == 'Y') Then
              Write(8,110) OLDPPFILE
            End If
          Else
            ! Archive all other selected unit numbers
            If (TYPE_LETTER_1(NFTUNIT) == 'b') Then
              Write(8,130) OLDPPFILE
            Else
              Write(8,110) OLDPPFILE
            End If
          End If

        End If

        ! Delete selected files
        If (FT_SELECT(NFTUNIT) == 'Y') Then
          Write(8,150) OLDPPFILE
        End If

#if defined(T3E)
        ! DEPENDS ON: um_fort_flush
        Call um_fort_flush(8, icode)
#else
        Close(8)
        Open(8, file=filename)
#endif

      End If ! If (mype == 0)

      ! Format statements for post-processing options above
110   Format('%%% ',A14,' ARCHIVE PPNOCHART')
130   Format('%%% ',A14,' ARCHIVE BNDY')
150   Format('%%% ',A14,' DELETE')

      !  1.3 Construct filename.
      ! 
      !      For reinitialised files whose reinitialisation period is not
      !      a multiple of one hour, use 'Timestep' or 'Sub-hourly'
      !      filenaming convention rather than the globally defined one.
      ! 

      If (FT_STEPS(NFTUNIT) >  0 .And.                        &
         Mod(FT_STEPS(NFTUNIT)*SECS_PER_STEP,3600) /= 0) Then
        If (FT_STEPS(NFTUNIT)*SECS_PER_STEP >  3600) Then
          LOCAL_TC = 'Timestep         '
        Else
          ! Use "Sub-hourly" filenaming time convention for this case.
          LOCAL_TC = 'Sub-hourly       '
        End If
      Else
        LOCAL_TC = TIME_CONVENTION
      End If

      ! 
      !  1.3.1 Input files.
      ! 
      If (FT_INPUT(NFTUNIT) == 'Y') Then
        ! DEPENDS ON: get_name
        Call GET_NAME(EXPT_ID_IN,JOB_ID_IN,SMID,MeanLevel,TOGGLE,     &
           FT_STEPS(NFTUNIT),TYPE_LETTER_1(NFTUNIT),                  &
           TYPE_LETTER_3(NFTUNIT),                                    &
           MODEL_STATUS,LOCAL_TC,AnalysisHours,PPNAME,ICODE,CMESSAGE, &
           LCAL360)
        If (ICODE >  0) Goto 9999
      Else
        ! 
        !  1.3.2 Output files.
        ! 
        ! DEPENDS ON: get_name
        Call GET_NAME(EXPT_ID,JOB_ID,SMID,MeanLevel,TOGGLE,           &
           FT_STEPS(NFTUNIT),TYPE_LETTER_1(NFTUNIT),                  &
           TYPE_LETTER_3(NFTUNIT),                                    &
           MODEL_STATUS,LOCAL_TC,AnalysisHours,PPNAME,ICODE,CMESSAGE, &
           LCAL360)
        If (ICODE >  0) Goto 9999
      End If

      ! 
      !  1.4 Open file on unit NFTUNIT
      ! 
      LEN_PPNAME=Len(PPNAME)
      Write(6,*)'PPCTL:2.5 Closing file ',PPNAME,' on unit ',NFTUNIT
      ! DEPENDS ON: file_close
      Call FILE_CLOSE(NFTUNIT,PPNAME,LEN_PPNAME,FileNameProvidedExplicitly, &
         DoNotDeleteFile,ICODE)
      Write(6,*)'PPCTL:3 Opening new file ',PPNAME,' on unit ',NFTUNIT
      LEN_PPNAME=Len(PPNAME)
      ! DEPENDS ON: file_open
      Call FILE_OPEN(NFTUNIT,PPNAME,LEN_PPNAME,OpenReadWrite, &
         FileNameProvidedExplicitly,ICODE)
      If (ICODE /= 0) Then
        CMESSAGE='PPCTL   : Error opening new PPfile'
        GO TO 9999   !  Return
      End If

      ! 
      !  1.5 Update history file record for appropriate unit if 
      !      new filename
      ! 
      STRING=MODEL_FT_UNIT(NFTUNIT)
      STRING(11:17)='$DATAM/'
      STRING(18:31)=PPNAME
      STRING(32:80)='                                          '
      MODEL_FT_UNIT(NFTUNIT)=STRING

      ! 
      !  1.6 Initialise the direct access LOOKUP headers if OUTPUT file
      ! 
      !  (a) PP files
      If (TYPE_LETTER_1(NFTUNIT) == 'p' .Or. &
          TYPE_LETTER_1(NFTUNIT) == 'c') Then

#if defined(ATMOS)
        If (SMID == a_im .And. FT_OUTPUT(NFTUNIT) == 'Y') Then
          Write(6,*)'PPCTL:3 Initialising new file on unit ',NFTUNIT
          ! DEPENDS ON: init_pp
          Call INIT_PP(NFTUNIT,TYPE_LETTER_1(NFTUNIT),          &
             LEN1_LOOKUP,PP_LEN2_LOOK(NFTUNIT),                 &
             A_FIXHD,A_INTHD,A_REALHD,A_LEVDEPC,                &
             A_ROWDEPC,A_COLDEPC,                               &
             LEN_FIXHD,A_LEN_INTHD,A_LEN_REALHD,A_LEN1_LEVDEPC, &
             A_LEN2_LEVDEPC,A_LEN1_ROWDEPC,A_LEN2_ROWDEPC,      &
             A_LEN1_COLDEPC,A_LEN2_COLDEPC,                     &
             PP_LEN_INTHD, PP_LEN_REALHD,                       &
             ICODE,CMESSAGE)
        End If
#endif
        ! 
        !  (b) Boundary files
        ! 
      Else If (TYPE_LETTER_1(NFTUNIT) == 'b') Then
        !
        If (FT_OUTPUT(NFTUNIT) == 'Y') Then
          Write(6,*)'PPCTL: Initialising new boundary file on unit',NFTUNIT

          ! Get interface area number
          ! DEPENDS ON: intf_area
          Call intf_area( SMID, NFTUNIT, JINTF)

#if defined(ATMOS)
          If ( SMID  ==  a_im ) Then
            ! DEPENDS ON: in_intf
            Call IN_INTF (              &
#include "argduma.h"
#include "arginfa.h"
               NFTUNIT,ICODE,CMESSAGE)

            If (ICODE >  0) Then
              Write (6,*) ' PPCTL : Error in IN_INTF - Atmos.'
              GO TO 9999  !  Return
            End If

          End If  !  if SMID
#endif

          ! This is where a new boundary input file should be read in
        End If
      End If
      !
      FT_LASTFIELD(NFTUNIT)=0
      ! Close unit to release IO buffer if later reinit indicated
      LEN_PPNAME=Len(PPNAME)
      ! DEPENDS ON: file_close
      Call FILE_CLOSE(NFTUNIT,PPNAME,LEN_PPNAME,FileNameProvidedExplicitly, &
         DoNotDeleteFile,ICODE)
      ! Set FT_ACTIVE if initialising first file of a sequence
      ! (i.e. not initialised from INITIAL but within the run)
      !

      If (SMID == a_im) Then
        STEP = STEPim(a_im)
      End If
      !
      If (STEP-1 == FT_FIRSTSTEP(NFTUNIT)) Then
        !
        FT_ACTIVE(NFTUNIT)='Y'
        !
      Else If (TYPE_LETTER_1(NFTUNIT) == 'b') Then

        ! Get interface area number
        ! DEPENDS ON: intf_area
        Call intf_area( SMID, NFTUNIT, JINTF)
        If (STEP-1+INTERFACE_STEPSim(JINTF,A_IM)  == &
           FT_FIRSTSTEP(NFTUNIT)) Then

          FT_ACTIVE(NFTUNIT)='Y'

        End If

      End If
      
    End If ! If (LPP_SELECT(NFTUNIT))

  End Do ! NFTUNIT loops over NUNITS

9999 Return
End Subroutine PPCTL_REINIT
#endif
